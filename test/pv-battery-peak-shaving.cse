/*
This module demonstrates a simplistic "peak shaving" algorithm that uses an external signal to set the power threshold above which the battery will begin discharging. In essence, the battery attempts to limit the peak within its capability.

The battery is called to discharge when peak power (average peak power over the hour) is over a given threshold. Otherwise, it always charges when storage capacity is available.

NOTE: if CSE is built with the _DEBUG symbol defined, then you will get a warning when the gnPower of BuildingLoad goes negative. The GAIN object is not typically used in the fashion we present here: we merely want to specify the "effective" building load + PV without clutting this file with an entire building definition (as you would do in a real study).
*/
IMPORTFILE Data
  imFileName="pv-bt-data.csv"
  imFreq="Hour"
  imTitle="PV/BT Data"

WfName="Boulder.epw"
wuDays=0
nSubSteps=1

METER Elec0

#define kw_to_btu_per_hr 3412.142
#define zone_area 2100.0

ZONE Z1
  znArea = zone_area
  znVol = zone_area * 8
  GAIN BuildingLoad
      gnMeter = Elec0
      gnEndUse = Lit
      gnPower = import(Data,"AdjustedLoad_kW") * kw_to_btu_per_hr

#define max_bt_pwr 4.0
// Uncomment the line below and comment the line two below for a data driven peak adjustement:
// #define pwr_target_kW max(import(Data,"PeakAdjustedLoad_kW") - 4.0, 0.0)
#define pwr_target_kW 4.0

BATTERY "Battery"
  btMeter = Elec0
  btEndUse = BT
  btMaxCap = 16.0 // kWh
  btMaxChgPwr = max_bt_pwr // kW
  btMaxDschgPwr = max_bt_pwr // kW
  btUseUsrChg = Yes
  btChgEff = 0.97467943
  btDschgEff = 0.97467943
  btInitSOE = 1.0 // 100% state of energy
  btChgReq = -1.0 * select(
    import(Data,"AdjustedLoad_kW") > pwr_target_kW,
      import(Data,"AdjustedLoad_kW") - pwr_target_kW,
    import(Data,"AdjustedLoad_kW") < 0.0,
      import(Data,"AdjustedLoad_kW"),
    default 0.0)

EXPORTFILE "pv_bt_out_peakshave"
  xfFileName = "pv_bt_out_peakshave"
  xfFileStat = OVERWRITE

EXPORT
  exExportFile = "pv_bt_out_peakshave"
  exType = UDT
  exFreq = HOUR
  exTitle = "Battery Output (Peak Shaving)"
  exDayBeg = jan 1
  exDayEnd = dec 31
  exHeader = ColumnsOnly
  ExportCol colhead="Day" colVal=@Top.dateStr
  ExportCol colhead="Hour" colVal=@Top.iHr
  ExportCol colhead="Battery SOE" colVal=@BATTERY["Battery"].soe
  ExportCol colhead="Battery Cycles" colVal=@BATTERY["Battery"].cycles
  ExportCol colhead="Battery Energy [kWh]" colVal=@BATTERY["Battery"].energy
  ExportCol colhead="Meter: Adjusted Load (Home - PVGen) [Btu/hr]" colVal=@METER["Elec0"].H.lit
  ExportCol colhead="Meter: Battery EndUse" colVal=@METER["Elec0"].H.bt
  ExportCol colhead="Meter: Total EndUse" colVal=@METER["Elec0"].H.tot

REPORT rpType=MTR rpMeter=Elec0 rpFreq=Month

RUN
