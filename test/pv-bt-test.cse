/*
BATTERY + PV TEST AND CONTROL STRATEGIES
========================================

This module seeks to test the BATTERY and PV records using various control strategies. The Battery and PVArray are simulated while a "typical" building load is specified from an external CSV file and set to a GAIN block using the gnPower field.

DEFAULT:
--------

The default algorithm that ships with the code. This algorithm simply takes the adjusted load (Home Load - PV Generation) and sets the battery request to satisfy that. If excess PV is available, the battery attempts to absorb all energy, otherwise, the battery attempts to provide all energy. The default strategy (as all strategies) is limited to the physical capabilities of the battery.

PEAK SHAVING:
-------------

The basic "peak shaving" algorithm demonstrated here compares the current adjusted load (Home - PV Generation) with a power setpoint. If above the setpoint, the battery will attempt to provide assistance if it can. In essence, the battery attempts to limit the peak within its capability.

The battery is called to discharge when peak power (average peak power over the hour) is over a given threshold. Otherwise, it always charges when storage capacity is available.

THERMOSTAT:
-----------

This module demonstrates a simplistic "thermostat" algorithm for the battery. Under this strategy, the battery system attempts to stay at the SOE (or above) by developing a "restoring force" that counteracts the load as the SOE deviates more and more from the target.

Sign convention is:

* P_load is positive if there is a net load required by the House after adjusting for any PV generation
* P_load is negative if there is an excess of PV power after satisfying house load
* P_req is positive if the battery is to be charged
* P_req is negative if the battery is to be discharged
* P_max_dschg is the maximum discharge power of the battery (a positive since it is an absolute value/magnitude)
* P_max_chg is the maximum charge power of the battery (a positive since it is an absolute value or magnitude)

P_req(P_load, P_max_chg, t) =
  -1 * if (P_load >= 0)
         if (SOE(t-1) > SOE_set)
           P_load
         else
           min(P_load, P_max_dschg) - (((SOE_set - SOE(t-1)) / SOE_set) * P_max_chg)
         end
       else
         P_load
       end

Recall that what we specify to the battery is a *request*. The battery will not charge or discharge more than it has capacity. Nor will it violate its specified maximum charge/discharge rate.

TDV-Attenuated Strategy:
------------------------

This strategy controls based on the TDV signal and the load with the following guidelines:

- Any available excess energy to charge the battery is always requested
- Discharge is made in proportion to the amount the actual TDV signal vs a reference

Sign convention is the same as above. TDV is the current step's TDV signal; TDV_ref is the global reference TDV signal (which could change over time). As seen below, as TDV attains the reference value and above, the battery supplies a full charge. When the TDV is less than the reference value, only a fraction of the load is attempted.

P_req(P_load, P_max_chg, TDV, TDV_ref) =
  -1 * if (P_load >= 0)
         min(P_load, P_max_dschg) * (min(TDV, TDV_ref) / TDV_ref) ** 2
       else
         P_load
       end

Specifically, above, if TDV is 1/2 of TDV_ref, then only 1/4 of the load is requested to be supplied. If TDV is 3/4 of TDV_ref, then only 9/16 of the load is requested to be supplied. If TDV is greater than or equal to TDV_ref, the full load is requested to be supplied. Thus, charging at higher TDVs is favored without neglecting low-loads completely.

NOTE: if CSE is built with the _DEBUG symbol defined, then you will get a warning when the gnPower of BuildingLoad goes negative. The GAIN object is not typically used in the fashion we present here: we merely want to specify the "effective" building load + PV without clutting this file with an entire building definition (as you would do in a real study).
*/
IMPORTFILE Data
  imFileName="pv-bt-data.csv"
  imFreq="Hour"
  imTitle="PV/BT Data"

WfName="Bishop.epw" // reference city for CA Climate Zone 16
wuDays=0
nSubSteps=1

#define kw_to_btuh 3412.142
#define max_bt_pwr 4.08
#define pwr_target_kW 4.0
#define soe_target 0.7
#define TDV_ref 27.0
#define AdjLoad_kW import(Data, "AdjustedLoad_kW")

METER "ElecMtrDefault"
GAIN "BuildingLoadDefault"
  gnMeter = ElecMtrDefault
  gnEndUse = Lit
  gnPower = import(Data,"HouseLoad_kW") * kw_to_btuh
BATTERY "BatteryDefault"
  btMeter = ElecMtrDefault
  btEndUse = BT
  btMaxCap = 16.320 // kWh
  btMaxChgPwr = max_bt_pwr // kW
  btMaxDschgPwr = max_bt_pwr // kW
  btUseUsrChg = No
  btChgEff = 0.97467943
  btDschgEff = 0.97467943
  btInitSOE = 1.0 // 100% state of energy
GAIN "PvDefault"
  gnMeter = ElecMtrDefault
  gnEndUse = PV
  gnPower = -1.0 * import(Data,"PvOut_kW") * kw_to_btuh

METER "ElecMtr00"
GAIN "BuildingLoad00" LIKE BuildingLoadDefault
  gnMeter = ElecMtr00
BATTERY "Battery00" LIKE BatteryDefault
  btMeter = ElecMtr00
  btUseUsrChg = Yes
  btChgReq = 0.0

METER "ElecMtr0"
GAIN "BuildingLoad0" LIKE BuildingLoadDefault
  gnMeter = ElecMtr0
BATTERY "Battery0" LIKE BatteryDefault
  btMeter = ElecMtr0
  btUseUsrChg = Yes
  btChgReq = 0.0
GAIN "Pv0" LIKE PvDefault
  gnMeter = ElecMtr0

METER "ElecMtrPeakShaving"
GAIN "BuildingLoadPeakShaving" LIKE BuildingLoadDefault
  gnMeter = ElecMtrPeakShaving
BATTERY "BatteryPeakShaving" LIKE BatteryDefault
  btMeter = ElecMtrPeakShaving
  btUseUsrChg = Yes
  btChgReq = -1.0 * select(
    AdjLoad_kW > pwr_target_kW,
      AdjLoad_kW - pwr_target_kW,
    AdjLoad_kW < 0.0,
      AdjLoad_kW,
    default 0.0)
GAIN "PvPeakShaving" LIKE PvDefault
  gnMeter = ElecMtrPeakShaving

METER "ElecMtrThermostat"
GAIN "BuildingLoadThermostat" LIKE BuildingLoadDefault
  gnMeter = ElecMtrThermostat
BATTERY "BatteryThermostat" LIKE BatteryDefault
  btMeter = ElecMtrThermostat
  btUseUsrChg = Yes
  btChgReq = -1.0 * select(
    AdjLoad_kW >= 0.0,
      select(
        @BATTERY["BatteryThermostat"].soeBegIvl > soe_target,
          AdjLoad_kW,
        default (
          min(AdjLoad_kW, max_bt_pwr)
            - (((soe_target - @BATTERY["BatteryThermostat"].soeBegIvl) / soe_target) * max_bt_pwr))),
    default AdjLoad_kW)
GAIN "PvThermostat" LIKE PvDefault
  gnMeter = ElecMtrThermostat

METER "ElecMtrTDV"
GAIN "BuildingLoadTDV" LIKE BuildingLoadDefault
  gnMeter = ElecMtrTDV
BATTERY "BatteryTDV" LIKE BatteryDefault
  btMeter = ElecMtrTDV
  btUseUsrChg = Yes
  btChgReq = -1.0 * select(
    AdjLoad_kW >= 0.0,
      min(AdjLoad_kW, max_bt_pwr) * pow(min(import(Data,"TDV_kBtu__kWh"), TDV_ref) / TDV_ref, 2),
    default AdjLoad_kW)
GAIN "PvTDV" LIKE PvDefault
  gnMeter = ElecMtrTDV

METER "ElecMtrExtCalc"
GAIN "BuildingLoadExtCalc" LIKE BuildingLoadDefault
  gnMeter = ElecMtrExtCalc
BATTERY "BatteryExtCalc" LIKE BatteryDefault
  btMeter = ElecMtrExtCalc
  btUseUsrChg = Yes
  btChgReq = import(Data, "BtPwrReq_kW")
GAIN "PvExtCalc" LIKE PvDefault
  gnMeter = ElecMtrExtCalc

#define CalcTDV_kBtuh( n ) (@METER[n].H.tot / kw_to_btuh) * import(Data,"TDV_kBtu__kWh")

EXPORTFILE "PvBtOut"
  xfFileName = "pv_bt_out"
  xfFileStat = OVERWRITE
  EXPORT
    exType = UDT
    exFreq = HOUR
    exTitle = "Battery/PV Output"
    exDayBeg = jan 1
    exDayEnd = dec 31
    exHeader = ColumnsOnly
    ExportCol colhead="Day" colVal=@Top.dateStr
    ExportCol colhead="Hour" colVal=@Top.iHr
    ExportCol colhead="PV Out [kW]" colVal=@PVArray["PvDefault"].acOut / kw_to_btuh
    ExportCol colhead="HomeLoad [kW]" colVal=@METER["ElecMtrDefault"].H.lit / kw_to_btuh
    ExportCol colhead="TDV [kBtu/kWh]" colVal=import(Data,"TDV_kBtu__kWh")
    ExportCol colhead="NoBtNoPv SOE" colVal=@BATTERY["Battery00"].soe
    ExportCol colhead="NoBtNoPv Energy [kWh]" colVal=@BATTERY["Battery00"].energy
    ExportCol colhead="NoBtNoPv GridLoad [kW]" colVal=@METER["ElecMtr00"].H.tot / kw_to_btuh
    ExportCol colhead="NoBtNoPv TDV [kBtu]" colVal=CalcTDV_kBtuh("ElecMtr00")
    ExportCol colhead="NoBt SOE" colVal=@BATTERY["Battery0"].soe
    ExportCol colhead="NoBt Energy [kWh]" colVal=@BATTERY["Battery0"].energy
    ExportCol colhead="NoBt GridLoad [kW]" colVal=@METER["ElecMtr0"].H.tot / kw_to_btuh
    ExportCol colhead="NoBt TDV [kBtu]" colVal=CalcTDV_kBtuh("ElecMtr0")
    ExportCol colhead="Dflt SOE" colVal=@BATTERY["BatteryDefault"].soe
    ExportCol colhead="Dflt Energy [kWh]" colVal=@BATTERY["BatteryDefault"].energy
    ExportCol colhead="Dflt GridLoad [kW]" colVal=@METER["ElecMtrDefault"].H.tot / kw_to_btuh
    ExportCol colhead="Dflt TDV [kBtu]" colVal=CalcTDV_kBtuh("ElecMtrDefault")
    ExportCol colhead="PkShv SOE" colVal=@BATTERY["BatteryPeakShaving"].soe
    ExportCol colhead="PkShv Energy [kWh]" colVal=@BATTERY["BatteryPeakShaving"].energy
    ExportCol colhead="PkShv GridLoad [kW]" colVal=@METER["ElecMtrPeakShaving"].H.tot / kw_to_btuh
    ExportCol colhead="PkShv TDV [kBtu]" colVal=CalcTDV_kBtuh("ElecMtrPeakShaving")
    ExportCol colhead="Thmst SOE" colVal=@BATTERY["BatteryThermostat"].soe
    ExportCol colhead="Thmst Energy [kWh]" colVal=@BATTERY["BatteryThermostat"].energy
    ExportCol colhead="Thmst GridLoad [kW]" colVal=@METER["ElecMtrThermostat"].H.tot / kw_to_btuh
    ExportCol colhead="Thmst TDV [kBtu]" colVal=CalcTDV_kBtuh("ElecMtrThermostat")
    ExportCol colhead="TDV SOE" colVal=@BATTERY["BatteryTDV"].soe
    ExportCol colhead="TDV Energy [kWh]" colVal=@BATTERY["BatteryTDV"].energy
    ExportCol colhead="TDV GridLoad [kW]" colVal=@METER["ElecMtrTDV"].H.tot / kw_to_btuh
    ExportCol colhead="TDV TDV [kBtu]" colVal=CalcTDV_kBtuh("ElecMtrTDV")
    ExportCol colhead="Ext SOE" colVal=@BATTERY["BatteryExtCalc"].soe
    ExportCol colhead="Ext Energy [kWh]" colVal=@BATTERY["BatteryExtCalc"].energy
    ExportCol colhead="Ext GridLoad [kW]" colVal=@METER["ElecMtrExtCalc"].H.tot / kw_to_btuh
    ExportCol colhead="Ext TDV [kBtu]" colVal=CalcTDV_kBtuh("ElecMtrExtCalc")

RUN

